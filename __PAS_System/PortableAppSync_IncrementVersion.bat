@echo off
:: #################################################################################################################################################################################
:: | [CRAFTED] Increment Version | Portable App Sync by CodeAnthem | Created: 22.07.2023
:: | [AUTHOR] CodeAnthem | [MODIFIED] 24.05.2024 | [VERSION] 1.00
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
:: | [DESCRIPTION]
:: |  Increment Version
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
:: | [NOTES]
:: |  Hint: Can't be run from network share with admin priviledges
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
:: #################################################################################################################################################################################


:: ### WORKFLOW
:: #################################################################################################################################################################################
    call:getHeader "Update Version"
	call:findVersionFile "%~dp0"
    echo.
    echo. Closing in 3 seconds
    timeout /t 3 /nobreak >nul
    exit /b 0
    exit
:: #################################################################################################################################################################################
  

:: ### FUNCTIONS: UI
:: #################################################################################################################################################################################
    :getHeader
       cls
       echo =====================================================================================================
       if "%~1" == "" (
          echo. Portable Apps Sync
       ) else (
          echo. Portable Apps Sync: %~1
          title PAS: %~1
       )
       echo =====================================================================================================
       echo.
    goto:eof
:: #################################################################################################################################################################################

:: ### FUNCTIONS: UPDATE VERSION FILE
:: #################################################################################################################################################################################
    :findVersionFile
        setLocal
        set count=0
        for /f %%f in ('dir /b /on /a-d "%~1\v*.version"') do set versionFile=%%~dpff && set /A count=count+1
		if %count% == 1 ( call:updateVersionFile "%versionFile%" ) else ( call:createVersionFile )
		endlocal
    goto:eof
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
    :createVersionFile
        echo "Creating new version file"
        echo >"v1.version" 1
	goto:eof
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
    :updateVersionFile
        SetLocal EnableDelayedExpansion
		set "versionFile=%~1"
    	echo. Version file found:
		echo. !versionFile!
		echo.
		
    	set /p currentVersion=<"!versionFile!"
    	set /a newVersion=currentVersion+1
		del "%versionFile%"
    	echo >"v!newVersion!.version" !newVersion!
    	echo. Updated version: !currentVersion! -^> !newVersion!
    goto:eof
:: #################################################################################################################################################################################
