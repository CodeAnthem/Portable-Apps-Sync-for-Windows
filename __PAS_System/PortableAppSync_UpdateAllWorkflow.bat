@echo off
:: #################################################################################################################################################################################
:: | [CRAFTED] PAS UPDATE ALL | Portable App Sync by CodeAnthem | Created: 22.07.2023
:: | [AUTHOR] CodeAnthem | [MODIFIED] 24.05.2024 | [VERSION] 1.00
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
:: | [DESCRIPTION]
:: |  Updater finder
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
:: | [NOTES]
:: |  Hint: Can't be run from network share with admin priviledges
:: ----------------------------------------------------------------------------------------------------------------------------------------------------------------
:: #################################################################################################################################################################################


:: ### WORKFLOW
:: #################################################################################################################################################################################
    call:getHeader "Update All"
    SetLocal EnableDelayedExpansion
    set /a counter=0
    set /a counter2=0
    for %%i in ("%~dp0..") do set "pathLocalRoot=%%~fi"
    for /f "tokens=*" %%f in ('dir /b /on /ad "%pathLocalRoot%"') do call:checkFolder "%%~f"
	 call:getHeader "Update All"
    echo. "Updated #%counter2%/%counter%"
    echo.
    echo. Closing in 2 seconds
    timeout /t 2 /nobreak >nul
    exit /b 0
    exit
:: #################################################################################################################################################################################
  

:: ### FUNCTIONS: UI
:: #################################################################################################################################################################################
    :getHeader
       cls
       echo =====================================================================================================
       if "%~1" == "" (
          echo. Portable Apps Sync
       ) else (
          echo. Portable Apps Sync: %~1
          title PAS: %~1
       )
       echo =====================================================================================================
       echo.
    goto:eof
:: #################################################################################################################################################################################

:: ### FUNCTIONS: UPDATE CALLER
:: #################################################################################################################################################################################
    :checkFolder
        if "%~1" == "__PAS_System" goto:eof
        set "scriptUpdater=%pathLocalRoot%\%~1\Updater.bat"
        echo "scriptUpdater %scriptUpdater%"
        set /a counter=!counter!+1
        if not exist "%scriptUpdater%" goto:eof
		  call:getHeader "Update All: %~1"
        call "%scriptUpdater%"
        set /a counter2=!counter2!+1
        cls
    goto:eof
:: #################################################################################################################################################################################
